use crate::{Binary as B, BinaryCode as BC, Unary as U, UnaryCode as UC, Value as V, VarName as VName, Expr as E};

grammar;

// ######### constant / variable #########
pub VarName: VName = { r"[a-zA-Z_]\w*" => <>.into() }
pub Value: V = {
    TRUE => true.into(),
    FALSE => false.into(),
    r"\d+" => V::Int(<>.parse().unwrap()),
    r"\d+\.\d+" => V::Float(<>.parse().unwrap()),
    r"\d+(\.\d+)?f" => V::Float(<>[..<>.len()-1].parse().unwrap()),
    r"'(\\[.]|[^\\'])'" => <>[1..<>.len()-1].chars().nth(0).unwrap().into(),
    r#""(([^"]|\\")*[^\\"])?""# => <>[1..<>.len()-1].into(),
    <VarName> => <>.into(),
}

// ######### atomic or wrapped #########
pub Atomic: E = {
    "(" <Expr> ")" => U::from((UC::Parentheses, <>)).to_expr(),
    "|" <Expr> "|" => U::from((UC::Abs, <>)).to_expr(),
    <val:Atomic> "[" <at:Expr> "]" => E::Index(val.boxed(), at.boxed()),
    <val:Atomic> "[" <start:Expr> ":" <end:Expr> "]" => E::Slice(val.boxed(), start.boxed(), end.boxed()),
    Value => <>.to_expr(),
}

// // ######### Tier 0: unary #########
pub UnaryOP: UC = {
    // Regexp isn't allowed, because all of these are valid variable names...
    "+" => UC::Plus,
    "-" => UC::Neg,
    URnd => UC::Random,
    USin => UC::Sin,
    UCos => UC::Cos,
    UTan => UC::Tan,
    UExp => UC::Exp,
    ULog => UC::Log,
    UArcsin => UC::Arcsin,
    UArccos => UC::Arccos,
    UArctan => UC::Arctan,
    UNot => UC::Not,
    UToUpper => UC::ToUpper,
    UIsWordChar => UC::IsWordChar,
    URound => UC::Round,
    UToLower => UC::ToLower,
    UIsDigit => UC::IsDigit,
    UToFloat => UC::ToFloat,
    UFloor => UC::Floor,
}
Tier0: E = {
    UnaryOP Tier0 => U::from((<>)).to_expr(),
    Atomic,
}

// ######### Tier 1: power and search #########
T1OP: BC = { "^" => BC::Pow, "@" => BC::Search }
Tier1: E = {
    <l:Tier1> <o:T1OP> <r:Tier0> => B::from((o, l, r)).to_expr(),
    Tier0,
};

// ######### Tier 2: Multiplicative #########
T2OP: BC = { "*" => BC::Mul, "/" => BC::Mul, "DIV" => BC::IntDiv, "MOD" => BC::Mod }
Tier2: E = {
    <l:Tier2> <o:T2OP> <r:Tier1> => B::from((o, l, r)).to_expr(),
    Tier1,
};

// ######### Tier 3: Additive #########
Tier3: E = {
    // hard coded these ops, this way they get priority over unary + and -
    <Tier3> "+" <Tier2> => B::from((BC::Add, <>)).to_expr(),
    <Tier3> "-" <Tier2> => B::from((BC::Sub, <>)).to_expr(),
    Tier2,
};

// ######### Tier 4: Relational #########
T4OP: BC = {
    "=" => BC::Eq,
    "/=" => BC::Ne,
    "<" => BC::Lt,
    "<=" => BC::Le,
    ">" => BC::Gt,
    ">=" => BC::Ge,
}
Tier4: E = {
    <l:Tier4> <o:T4OP> <r:Tier3> => B::from((o, l, r)).to_expr(),
    Tier3,
};

// ######### Tier 5: Logical #########
T5OP: BC = {
    BAnd => BC::And,
    BOr => BC::Or,
}
Tier5: E = {
    <l:Tier5> <o:T5OP> <r:Tier4> => B::from((o, l, r)).to_expr(),
    Tier4,
};

pub Expr: E = { Tier5 }

// !!!!!!!!!!!!!!!!!!!!!!!!!!!

match {
    // unary operators
    r"RND|rnd"  => URnd,
    r"SIN|sin"  => USin,
    r"COS|cos"  => UCos,
    r"TAN|tan"  => UTan,
    r"EXP|exp"  => UExp,
    r"LOG|log"  => ULog,
    r"NEM|nem|NOT|not" => UNot,
    r"ARCSIN|arcsin" => UArcsin,
    r"ARCCOS|arccos" => UArccos,
    r"ARCTAN|arctan" => UArctan,
    r"UPPER|upper|NAGY|nagy" => UToUpper,
    r"ROUND|round|KEREK|kerek" => URound,
    r"LOWER|lower|KICSI|kicsi" => UToLower,
    r"FLOOR|floor|EG[ÉE]SZ|eg[ée]sz" => UFloor,
    r"IS_CHAR|is_char|BETU|betu" => UIsWordChar,
    r"FLOAT|float|VAL[ÓO]S|val[óo]s"  => UToFloat,
    r"IS_DIGIT|is_digit|SZ[AÁ]M|sz[aá]m" => UIsDigit,
    // binary operators
    r"OR|or|VAGY|vagy" => BOr,
    r"AND|and|[ÉE]S|[ée]s" => BAnd,
    r"IGAZ|igaz|TRUE|True|true" => TRUE,
    r"HAMIS|hamis|FALSE|False|false" => FALSE,
} else {
    _
}
